(* Draft v0.0.1-20210218 *)
TranslationUnit = { ImportDeclaration } , { { Attribute } , ExportableConstDeclaration };
Attribute = "[" , Identifier , "]";
ExportableConstDeclaration = [ "export" , ["default"] ] , ConstDeclaration;

ConstDeclaration = "const" , DeclarationBody;
LetDeclaration = "let" , DeclarationBody;
DeclarationBody = Identifier , "=" , Expression , ";";

(* Expression = Literal | CallExpression | FunctionExpression; *)
Expression = LogicalORExpression;
CallExpression = Identifier , { "." Identifier } Args;
Args = "(" , [ { Expression , "," } , Expression ] ")";
FunctionExpression = "(" , ")" , "=>" , CompoundExpression;
ExpressionStatement = Expression , ";";
CompoundExpression = "{" , { (ExpressionStatement | ConstDeclaration | LetDeclaration) } , [ Expression ] , "}";

PrimaryExpression = 
    (Literal | Identifier | FunctionExpression | ParenthesizedExpression | CompoundExpression),
    { { ".", Identifier }, { Args } };
ParenthesizedExpression = "(" , Expression , ")";
UnaryExpression = 
    PrimaryExpression
    | ("+" , UnaryExpression)
    | ("-" , UnaryExpression)
    | ("!" , UnaryExpression)
    | ("~" , UnaryExpression);
ExponentiationExpression = UnaryExpression | (PrimaryExpression , "**", ExponentiationExpression);
MultiplicativeExpression = ExponentiationExpression, { MultiplicativeOperator, ExponentiationExpression };
AdditiveExpression = MultiplicativeExpression, { AdditiveOperator, MultiplicativeExpression };
ShiftExpression = AdditiveExpression, { ShiftOperator, AdditiveExpression };
RelationalExpression = ShiftExpression, { RelationalOperator, ShiftExpression };
EqualityExpression = RelationalExpression, { EqualityOperator, RelationalExpression };
BitwiseANDExpression = EqualityExpression, { "&", EqualityExpression };
BitwiseXORExpression = BitwiseANDExpression, { "^", BitwiseANDExpression };
BitwiseORExpression = BitwiseXORExpression, { "|", BitwiseXORExpression }; 
LogicalANDExpression = BitwiseORExpression, { "&&", BitwiseORExpression };
LogicalORExpression = LogicalANDExpression, { "||", LogicalANDExpression };

(* AssignmentStatement = Identifier , { "." Identifier } , AssignmentOperator, Expression; *)

NamedImportDeclaration = "import" , "{" , Identifier , { "," , Identifier } , "}" , "from" (DoubleQuotesString | SingleQuotesString);
DefaultImportDeclaration = "import" , Identifier , "from" , (DoubleQuotesString | SingleQuotesString);
ImportDeclaration = (NamedImportDeclaration | DefaultImportDeclaration) , ";";

Literal = Number | DoubleQuotesString | SingleQuotesString | Boolean;
Boolean = "true" | "false";

MultiplicativeOperator = "*" | "/" | "%";
AdditiveOperator = "+" | "-";
ShiftOperator = "<<" | ">>" | ">>>";
RelationalOperator = "<" | ">" | "<=" | ">=";
EqualityOperator = "==" | "!=";
AssignmentOperator = "=" | "*=" | "/=" | "%=" | "+=" | "-=" | "<<=" | ">>=" | ">>>=" | "&=" | "^=" | "|=" | "**=";

(* CompoundExpression: add const and expression declarations. *)
(* Expression: add expression for basic js *)